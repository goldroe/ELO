#import "libc/stdio.elo";

main :: () -> int {
    cells: [32][32]int;
    cells_copy: [32][32]int;

    cells[5][6] = 1;
    cells[5][7] = 1;
    cells[5][8] = 1;
    cells[6][7] = 1;

    map_width  := 32;
    map_height := 32;

    iterations := 100;

    fp := fopen("gol.out", "w");

    fprintf(fp, "*** GAME OF LIFE ***\n\n");
    fprintf(fp, "Iterations: %d\n", iterations);

    for i in 0..iterations {
        fprintf(fp, "========== IT:%d ============\n", i);
        for y in 0..map_height {
            for x in 0..map_width {
                if cells[y][x] {
                    fputc(cast(int)'*', fp);
                } else {
                    fputc(cast(int)' ', fp);
                }
            }
            fputc(cast(int)'\n', fp);
        }

        // copy cells
        for y in 0..map_height {
            for x in 0..map_width {
                cells_copy[y][x] = cells[y][x];
            }
        }

        for y in 0..map_height {
            for x in 0..map_width {
                live := 0;
                if x > 0 {
                    live += cells_copy[y][x-1];
                }
                if x < map_width - 1 {
                    live += cells_copy[y][x + 1];
                }
                if y > 0 {
                    live += cells_copy[y-1][x];
                    if x > 0 {
                        live += cells_copy[y-1][x-1];
                    }
                    if x < map_width - 1 {
                        live += cells_copy[y-1][x+1];
                    }
                }
                if y < map_height - 1 {
                    live += cells_copy[y + 1][x];
                    if x > 0 {
                        live += cells_copy[y+1][x-1];
                    }
                    if x < map_width - 1 {
                        live += cells_copy[y+1][x+1];
                    }
                }

                cell := *cells[y][x];

                if cell.* {
                    if live < 2 || live > 3 {
                        cell.* = 0;
                    }
                } else if live == 3 {
                    cell.* = 1;
                }
            }
        }
    }

    return 0;
}
