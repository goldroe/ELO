#load "stdio.elo"

main :: () -> int {
    cells: [32][32]int;
    cells_copy: [32][32]int;

    cells[5][6] = 1;
    cells[5][7] = 1;
    cells[5][8] = 1;
    cells[6][7] = 1;

    map_width  := 32;
    map_height := 32;

    iterations := 100;

    fp := fopen("gol.out", "w");

    fprintf(fp, "*** GAME OF LIFE ***\n\n");
    fprintf(fp, "Iterations: %d\n", iterations);

    for i := 0; i <= iterations; i += 1 {
        fprintf(fp, "========== IT:%d ============\n", i);
        for y := 0; y < map_height; y += 1 {
            for x := 0; x < map_width; x += 1 {
                ch := 0x20;
                if cells[y][x] {
                    ch = 0x2A;
                }
                fputc(ch, fp);
            }
            fputc(0xA, fp);
        }

        // copy cells
        for y := 0; y < map_height; y += 1 {
            for x := 0; x < map_width; x += 1 {
                cells_copy[y][x] = cells[y][x];
            }
        }

        for y := 0; y < map_height; y += 1 {
            for x := 0; x < map_width; x += 1 {
                live := 0;
                if x > 0 {
                    live += cells_copy[y][x-1];
                }
                if x < map_width - 1 {
                    live += cells_copy[y][x + 1];
                }
                if y > 0 {
                    live += cells_copy[y-1][x];
                    if x > 0 {
                        live += cells_copy[y-1][x-1];
                    }
                    if x < map_width - 1 {
                        live += cells_copy[y-1][x+1];
                    }
                }
                if y < map_height - 1 {
                    live += cells_copy[y + 1][x];
                    if x > 0 {
                        live += cells_copy[y+1][x-1];
                    }
                    if x < map_width - 1 {
                        live += cells_copy[y+1][x+1];
                    }
                }

                cell := *cells[y][x];
                alive := cell.*;

                if alive == 1 {
                    // under population
                    if live < 2 {
                       cell.* = 0;
                    }
                    // over population
                    if live > 3 {
                       cell.* = 0;
                    }
                }

                if alive == 0 {
                    // birth
                    if live == 3 {
                        cell.* = 1;
                    }
                }
            }
        }
    }

    return 0;
}
